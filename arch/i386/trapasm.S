#include <arch/i386/mmu.h>

# vectors.S sends all traps here.
.globl alltraps
alltraps:

    # 1. Build up trap frame
    pushl %ds
    pushl %es
    pushl %fs
    pushl %gs
    pushal

    # 2. Set up data segments
    movw $(SEG_SELECTOR(SEG_KDATA, 0, 0)), %ax
    movw %ax, %ds
    movw %ax, %es

    # 3. Call trap(tf)
    pushl %esp
    call trap
    addl $4, %esp
    # Since an argument for trap is pushed onto the stack 
    # trap() generated by gcc is a "cdecl" function, which require caller to clean the arguments,
    # while "stdcall" function requires callee to clean arguments on stack


# Return falls through to trapret...
.globl trapret
trapret:
	# Restore registers.
	# Your code here:

    popal
    popl %gs
    popl %fs
    popl %es
    popl %ds

    addl $8, %esp
    # Since we have push two argument in vector.S

	iret
