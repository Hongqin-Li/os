#include <mmu.h>

# vectors.S sends all traps here.
.globl alltraps
alltraps:
    cli

    # 1. Build up trap frame
    pushl %ds
    pushl %es
    pushl %fs
    pushl %gs
    pushal

    # 2. Set up data segments
    movw $(SEG_SELECTOR(SEG_KDATA, 0, 0)), %ax
    movw %ax, %ds
    movw %ax, %es

    # 3. Call trap(tf)
    pushl %esp
    call trap
    addl $4, %esp
    # Since an argument for trap is pushed onto the stack 
    # trap() generated by gcc is a "cdecl" function, which require caller to clean the arguments,
    # while "stdcall" function requires callee to clean arguments on stack


# Return falls through to trapret...
.globl trapret
trapret:
	# Restore registers.
    popal
    popl %gs
    popl %fs
    popl %es
    popl %ds

    addl $8, %esp
    # Pop error code and trapno
    # iret don't know what the current trap is,
    # thus won't pop those for us
    sti
	iret
